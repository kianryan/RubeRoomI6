PuzzleRoom earth_room "Southern Cavern"
    with description[;
        print "This large cavern is covered in a vast array of green and luscious plant
        life.  Natural light floods in from a hole in the top of cavern, bringing a
        vibrancy to unexpected waves of colours in the room.  In the centre, a ring
        of boulders provides a convenient place to rest oneself while taking in the
        spendlours of the plant life. ^^";

        print "The paassage to the ";
        VorpleLinkCommand("north");
        print " leads back to the atrium.^^";

        if (parent(troll) == self)
            print "^^Sat on one of the boulders, hunched over himself and head resting
            on a closed fist, is what appears to be a abstract, rocky human statue.";
        ],
        win_description[;
            move earth_key to earth_room;
        ],
    n_to cube_room;


Prop boulder "Boulder" earth_room
    with name 'boulder',
    with description
        "A ring of boulders sits in the middle of the room.  They're well sized
        to sit on, and the small indents in the middle hint that that's what
        they've been used for for a long time.",
    has pluralname;

!============================================================================
! NPC
Class NPC
    with life [;
        Ask, Order : print_ret "Use either Talk or Answer";
        ],
    has animate;

NPC troll "Rodin" earth_room
    with name 'rodin' 'troll' 'rocky' 'statue',
        description
            "Towering twice the size of a man, and made from what looks like loosely
            pieces of held together rock, a troll sits on one of the boulders in the
            middle of this cavern.  Every so often he sighs to himself.^^",
        initial [;
            print "^The statue slowly tilts his head and gives you a surprisingly deep look.^";
            if (location hasnt visited) {
                print "^U. ME. RODIN.^^
                THOUGHT DEEP. DEEP THOUGHT. SSH.^^";
            }
        ],
        greeting "HELLO THERE, NOW WE CAN TALK",
        life [;
            Talk:
                if (~~(self.greeted)) "U RUDE. HELLO."; ! don't be rude.
                if (self.long_intro) {
                    print "I BEEN HERE LONG TIME.^
                    WIZARD ASKED TO THINK ON BIG PROBLEM. ^
                    NO GO UNTIL PROBLEM SOLVED. ^
^
                    (You notice there's moss reaching from the bottom^
                    of his feet spreading to floor around him.)^^";
                    self.long_intro = false;
                }
                else {
                    print "The troll clears his throat.^^";
                }
                print_ret "The troll carefully unfurls a rolled up piece
                of parchment held in his hand, furls his brow and slowly
                reads the words out to you.^
                ^
                'SAI MAI NAME UND I DIS-APP-EAR. WOT AM I?'^
                ^
                The troll carefully rolls up the parcement again and
                keeps careful hold of it.";
            Answer:
                if (~~(self.greeted)) "U RUDE."; ! don't be rude.
                if (self.long_intro) {
                    print "WAT";
                }
                else if (noun == 'silence') {
                    print "'HEH.'^
                    ^
                    The troll disappears in a cloud along with the silence. ^^
                    Something is left behind on the boulder where he was sat.";
                    remove self;
                    earth_room.win_room();
                    rtrue;
                } else {
                    "The troll looks at you quizzacly, but nothing happens.";
                }
           ],
    with long_intro true,
    with greeted false, ! you must greet first.
    has male proper;