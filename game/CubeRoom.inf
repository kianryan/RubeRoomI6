! Does each face in the room have a key?
[
    AllKey room a o;
    a = true;
    objectloop(o in room) {
        if (o ofclass MachineFace) {
            if (children(o) == 0)
                a = false;
        }
    }
    return a;
];

PuzzleRoom cube_room "The Atrium"
    with description
        [;
            print "A central atrium carved from the rock houses an intricate man made machine
        shaped in to a cube.  It's fashioned from components of brass and gold.  The machine
        is about as high and wide as a person.^^";
            if (self.room_scored == true) {
                print "The machine, now stands on stilts in the air.  The space below the machine
                houses a staircase leading ";
                VorpleLinkCommand("down");
                print "^^";
            } else {
                print "The ";
                VorpleLinkCommandText("examine machine", "machine");
                print " looks ... complicated.^^";
            }
            print "Passages lead off to the ";
            VorpleLinkCommand("north");
            print ", ";
            VorpleLinkCommand("south");
            print ", ";
            VorpleLinkCommand("east");
            print " and ";
            VorpleLinkCommand("west");
            new_line;
        ],
        win_description
        [;
            print
                "^^
                --^^
                ...As you fit the last piece of the puzzle together, the umbrella slowly starts
                to turn.  You dash to the north side to watch the somewhat hypnotic umbrella's
                canopy slowly and in its stead, turn the cogs on the surface on the machine.
                Tracing the lines you follow it around to the east side.^^

                On the east side at the very top you just catch the small brass boot wind up
                and give the marble an almightly kick.  The marble fires off down the helter
                skelter of tubing, building up more and more speed until it reaches the turn
                to the south side. ^^

                Following it around to the south side you see it shoot past the flint cog,
                causing a few sparks, just enough to light the bone try tinder to either side.
                As the tinder burns away wooden blocks behind the tinder are exposed
                and start to glow. In a couple of minutes bubbles start to form inside the
                spiral glass tubing as the water start to boil.  You hear sputtering from
                the west side.^^

                Following the cube round, as the water boils a thin jet of steam blows through the sails of
                the little windmill.  As the windmill turns, the shaft below chops the
                water in the tray causing the little boat to rhythmiclly bob up and down as it
                slowly makes it's way to the little shore at the end.^^

                Finally the little boat reaches the shore, the whole machine slows to a halt, and
                for a few seconds you wonder what that was all about.  The floor rumbles and the
                entire machine lifts away from the ground on ornate pillars exposing a staircase
                leading down...^^";

            remove north_face;
            remove east_face;
            remove south_face;
            remove west_face;
            rtrue;
        ],

        n_to fire_room,
        s_to earth_room,
        e_to water_room,
        w_to air_room,
        d_to staircase;

!  We need to ensure that keys are placed in the right spots on
!  each side.
!  We then open the umbrella to turn it and kick the machine off...
! Oh, is each face actually it's own item?
! That would work quite nicely, then each face is it's own test.

Prop machine "Machine" cube_room
    with name 'machine' 'cube',
        description
        [;
            if (cube_room.room_scored == true) {
                "The machine is now out of your reach, high on ornate stilts.  The
                staircase sits below it.";
           } else {
                print "You take a closer look at the machine in the center of the room.  It has
                four sides (";
                VorpleLinkCommandText("examine north face", "facing north, ");
                VorpleLinkCommandText("examine south face", "south, ");
                VorpleLinkCommandText("examine east face", "east, ");
                VorpleLinkCommandText("examine west face", "and west");
                "), each tightly faced with
                cogs, worm gears and drives.^";
            }
        ];

Prop staircase "Staircase"
    with name 'staircase' 'stairs',
        description
        "An ornate spiral staircase, with a handrail, leads in to the darkness below.
        The slats are made of perforated metal and quite solid, but you get the feeling
        that if you go down here, you're not coming back.",
   door_to bottom_staircase,
    door_dir d_to,
    has door;

Class MachineFace
    with description
        [;
            if (parent(self.right_key) == self) {
                self.key_fitted_description();
            } else {
                self.no_key();
           }
        ],
       key_fitted false,
       after [;
            Receive:
                if (noun == self.right_key) {
                    if (self.key_fitted == false) {
                        self.key_fitted = true;
                        give self ~open;
                        score = score + 1;
                        self.key_fitted_description();
                    }
                    if (AllKey(cube_room)) {
                        move staircase to cube_room;
                        give staircase open;
                        cube_room.win_room();
                    }
                    rtrue;
                } else {
                    move noun to player;
                    print_ret "I can't work out how to fit that here.";
                }
        ],
    has scenery supporter open;


MachineFace north_face "North Face" cube_room
    with name 'north' 'face' 'side' 'cube',
        right_key air_key,
        key_fitted_description "An opened umbrella now protrudes from this face.  The cog at the base
        of the umbrella fits neatly against the cog on the surface, making a complete
        chain of cogs, screws and cams winding their way to the east side of the cube.^",
        no_key "The north side has a small round hole in the very centre of the cube.
                One cog lines up close to the hole, with a number interfacing to a series
                of screws cogs, and cams winding their way to the east side of the cube.^";

MachineFace east_face "East Face" cube_room
   with name 'east' 'face' 'side' 'cube',
        right_key earth_key,
        key_fitted_description "A small marble is now balanced at the start of the long brass tube.
                Any attempt to move it brings it right back to it's start position,
                you can't give it enough force to slide it down the tube.  A series
                of tilted brass tubes run from the top of the cube to around the
                mid point.  The small brass boot stands poised waiting.  The bottom
                of the tube turns on to the south side.",
        no_key "The east side of the cube consists of a series of tilted brass tubes
                running from the top of the cube to around the mid point.  The very top
                tube is completely flat with a hole at the beginning.  What appears to
                be a tiny metal boot is lined up with the start of the tube, connected
                to the gears coming from the north side.  The bottom of the tube turns
                on to the south side.^";

MachineFace south_face "South Face" cube_room
    with name 'south' 'face' 'side' 'cube',
        right_key fire_key,
        key_fitted_description "On the south side the tube continues on to the lower half of the machine.
                There's a small opening at the mid way point of the tube, and on the spindle
                is now a cog of flint locked in to place. Small steel cages around
                the spindle are packed with dried straw.  Above the spindle is a spiral
                of beautifully blown glass open at the top end, standing upright.  Water
                sits inside the spiral.  At the top of the glass spiral, the glass tubing
                turns straight and turns on the west side.^",
        no_key "On the south side the tube continues on the lower half of the machine.
                There's a small opening at the mid way point of the tube, and a spindle
                just above the opening to attach something to. Small steel cages around
                the spindle are packed with dried straw.  Above the spindle is a spiral
                of beautifully blown glass open at the top end, standing upright.  Water
                sits inside the spiral.  At the top of the glass spiral, the glass tubing
                turns straight and turns on the west side.^";

MachineFace west_face "West Face" cube_room
    with name 'west' 'face' 'side' 'cube',
        right_key water_key,
        key_fitted_description "On the west side the glass tubing stops suddenly before a tiny
                gold windmill.  The windmill is at the very top of the cube out of your
                reach and a single shaft extends down into a tub of water at the bottom.
                There's a platform at the end of the tub of water, and the sides are
                extended to quite a height.  You've dropped the small wooden boat
                in the little lake, it bobs away merrily.^",
        no_key "On the west side the glass tubing stops suddenly before a tiny
                gold windmill.  The windmill is at the very top of the cube out of your
                reach and a single shaft extends down into a tub of water at the bottom.
                There's a platform at the end of the tub of water, and the sides are
                extended to quite a height.  You can't reach in to the water in the tub.^";