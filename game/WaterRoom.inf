PuzzleRoom water_room "Eastern Cavern"
    with description
        [;
            print "You hear the water before you enter the room.  As you walk in to the
                room, you see the water falling from a waterfall in to a small rock
                pool below.^^";
            print "The paassage to the ";
            VorpleLinkCommand("west");
            print_ret " leads back to the atrium.^^";
        ]
   with win_description
    [;
        move water_key to water_room;
        "The scale wobbles for a few seconds and eventually settles perfectly balanced.  ^^
        You can feel through the ground something hidden move, and then a small wooden
        sailboat floats out across the rockpool and lands on the shore.";
    ],
    w_to cube_room;

Prop waterfall "Waterfall" water_room
    with name 'waterfall'
    with description
        "The water is coming from between the rocks in the darkness above
        and drops with a roaring flourish.  It's forcefull, but has an elegant
        calm to it.";

Attribute fillable; ! A container that can be filled
Attribute liquid; ! Contains an infinite liquid that can be used to fill a fillable

Prop rockpool "Rock Pool" water_room
    with name 'rock' 'pool' 'pond' 'rockpool',
    with description
        "As you peer in to the rock pool you can't tell how deep it goes.  The
        light fades away in to nothingness.  What you can see is absolutely clear
        and beautiful.  There is no life in the pool.  The rocks are dark and
        volcanic, and would cut you in you ventured in."
    has liquid;

Class Amphora
    with name 'amphora' 'jug' 'bucket' 'vase',
        description [;
            print "A roman styled amphora.  It holds exactly ", self.volume, " litres.^";
            if (self.contents == 0)
                print_ret "You peer inside, there's not a drop to be seen.";
            if (self.contents >= self.volume)
                print_ret "The amphora is full to the brim.";
            else
                print_ret "Some liquid sloshes around inside, but you can't tell how much.";
        ],
        volume 5,
        contents 0,
   has fillable;

Amphora "large amphora" water_room
    with name 'large'
    with volume 5;

Amphora "small amphora" water_room
    with name 'small'
    with volume 3;

Object scale "scale" water_room
    with name 'scale' 'scales',
    with capacity 1,
    with description [weighing;
        print "A pair of elegant brass scales.^";
        weighing = child(self);
        if (weighing == nothing) print_ret "There is nothing on the scale";
        if (weighing.contents > self.balance) print_ret "The scale tips heavy";
        if (weighing.contents < self.balance) print_ret "The scale tips light";
        print_ret "The scale is perfectly balanced";
    ],
        balance 4,
        room_scored false,
        after [;
            Receive:
                if (noun hasnt fillable) {
                    move noun to player;
                    print_ret "That doesn't fit on the scales";
                }
                if (noun.contents > self.balance) print_ret "The scale wobbles and tips heavy.";
                if (noun.contents < self.balance) print_ret "The scale wobbles and tips light.";
                return parent(self).win_room();
        ],
   has static supporter;

! Puzzle description - Attempt to light the lantern with the match.
! If you light one lantern, the two adjacent go out.  The aim is to light
! All of the lanterns.

